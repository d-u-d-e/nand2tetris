// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:
    
    // do operations on x
    Mux16(a=x, b[0..15]=false, sel=zx, out=x1); //zx
    Not16(in=x1, out=x2);
    Mux16(a=x1, b=x2, sel=nx, out=x3); //nx

    // do operations on y
    Mux16(a=y, b[0..15]=false, sel=zy, out=y1); //zx
    Not16(in=y1, out=y2);
    Mux16(a=y1, b=y2, sel=ny, out=y3); //nx

    // compute f and/or negate out
    And16(a=x3, b=y3, out=o1);
    Add16(a=x3, b=y3, out=o2);
    Mux16(a=o1, b=o2, sel=f, out=o3);
    Not16(in=o3, out=o4);
    // also compute ng flag
    Mux16(a=o3, b=o4, sel=no, out=out, out[15]=ng, out[0..7]=out1, out[8..15]=out2);

    // compute zr flag
    Or8Way(in=out1, out=zr1);
    Or8Way(in=out2, out=zr2);
    Or(a=zr1, b=zr2, out=zr3);
    Not(in=zr3, out=zr);
}