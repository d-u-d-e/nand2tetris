// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl
/** 
 * The Memory chip implements the complete address space of the Hack data memory,
 * including the RAM and memory mapped I/O.
 * Outputs the value of the memory location specified by the address input.
 * If (load == 1), sets the memory location specified by the address input
 * to the value of the in input.
 * Address space rules:
 * Only the upper 16K + 8K + 1 words of the memory are used.
 * Access to address 0 to 16383 results in accessing the RAM;
 * Access to address 16384 to 24575 results in accessing the Screen memory map;
 * Access to address 24576 results in accessing the Keyboard memory map.
 */ 
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    Not(in=address[14], out=readRAM);
    Not(in=address[13], out=o1);
    And(a=address[14], b=o1, out=readScreen);

    Or(a=readRAM, b=readScreen, out=readKbdNeg);
    Not(in=readKbdNeg, out=readKbd);

    And(a=readRAM, b=load, out=loadRAM);
    And(a=readKbd, b=load, out=loadKbd);
    And(a=readScreen, b=load, out=loadScreen);

    RAM16K(in=in, load=loadRAM, address=address[0..13], out=outRAM);
    Screen(in=in, load=loadScreen, address=address[0..12], out=outScreen);
    Keyboard(out=outKbd);
    Mux16(sel=readRAM, a=outScreen, b=outRAM, out=out1);
    Mux16(sel=readKbd, a=out1, b=outKbd, out=out);
}
